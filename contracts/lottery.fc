#include "imports/stdlib.fc";


;; ===== STORAGE =====
global slice storage::admin_address; ;; 267
global int storage::public_key; ;; 256

global int storage::commission_game_admin; ;; 16
global int storage::commission_game_runners; ;; 16
global int storage::min_bet; ;; 124
global int storage::max_bet; ;; 124
global int storage::max_participates; ;; 8
global int storage::participates_number; ;; 8 
global int storage::game_time; ;; 32
global int storage::game_start_time; ;; 32
global int storage::game_round; ;; 32
global int storage::stop_game_on_round; ;; 32 
global int storage::jackpot; ;; 124
global slice storage::last_game_winner_address; ;; 267
global cell storage::list_participates; ;; ref
global cell storage::last_game_list_participates; ;; ref 

;; ===== CONST ===== 
const int commission_divider = 10000;
const int storage_commission = 50000000;

;; ===== ERRORS =====
const int error::not_admin = 500;
const int error::invalid_signature = 501;
const int error::wrong_round = 502;
const int error::game_pause = 503;
const int error::game_is_not_over_yet = 504;

;; ===== OP-CODES =====
const int op::stop_game = 1;
const int op::change_sittings = 2;
const int op::change_admin = 3;
const int op::update_code_and_data = 4;

;; ===== FIFT =====
tuple get_prev_block() asm "PREVKEYBLOCK";
;; (cell, (cell, int)) ~udict_add_get_ref?(cell dict, int key_len, int index, cell value) asm(value index dict key_len) "DICTUADDGETREF" "NULLSWAPIF";
;; (cell, int) udict_replace_ref?(cell dict, int key_len, int index, cell value) asm(value index dict key_len) "DICTUREPLACEREF";
;; (cell, (int, cell, int)) ~udict_delete_get_min_ref?(cell dict, int key_len) asm(-> 0 2 1 3) "DICTUREMMINREF" "NULLSWAPIFNOT2";

(cell, (slice, int)) ~udict_add_get_builder?(cell dict, int key_len, int index, builder value) asm(value index dict key_len) "DICTUADDGETB" "NULLSWAPIF";
(cell, int) udict_replace_builder?(cell dict, int key_len, int index, builder value) asm(value index dict key_len) "DICTUREPLACEB";
(cell, (int, slice, int)) ~udict_delete_get_min?(cell dict, int key_len) asm(-> 0 2 1 3) "DICTUREMMIN" "NULLSWAPIFNOT2";
;; (slice, (slice, int)) ~try_load_msg_addr(slice s) asm ( -> 2 1 0) "LDMSGADDRQ" "NULLSWAPIFNOT" "s1 s2 XCHG";
(slice, (slice, int)) ~try_load_msg_addr(slice s) asm(-> 1 0 2) "LDMSGADDRQ" "NULLROTRIFNOT";


() load_data() impure inline {
    slice ds = get_data().begin_parse(); ;; Bits = 523. Refs = 1.
    storage::admin_address = ds~load_msg_addr();
    storage::public_key = ds~load_uint(256);

    slice ds2 = ds~load_ref().begin_parse();  ;; Bits = 815. Refs = 2.
    storage::commission_game_admin = ds2~load_uint(16);
    storage::commission_game_runners = ds2~load_uint(16);
    storage::min_bet = ds2~load_coins();
    storage::max_bet = ds2~load_coins();
    storage::max_participates = ds2~load_uint(8);
    storage::participates_number = ds2~load_uint(8);
    storage::game_time = ds2~load_uint(32);
    storage::game_start_time = ds2~load_uint(32);
    storage::game_round = ds2~load_uint(32);
    storage::stop_game_on_round = ds2~load_uint(32);
    storage::jackpot = ds2~load_coins();
    storage::last_game_winner_address = ds2~load_msg_addr();
    storage::list_participates = ds2~load_dict();
    storage::last_game_list_participates = ds2~load_dict();

    ds2.end_parse();
    ds.end_parse();
}


() save_data() impure inline {
    set_data(
        begin_cell()
            .store_slice(storage::admin_address)
            .store_uint(storage::public_key, 256)
            .store_ref( 
                begin_cell()
                    .store_uint(storage::commission_game_admin,16)
                    .store_uint(storage::commission_game_runners,16)
                    .store_coins(storage::min_bet)
                    .store_coins(storage::max_bet)
                    .store_uint(storage::max_participates, 8)
                    .store_uint(storage::participates_number, 8)
                    .store_uint(storage::game_time, 32)
                    .store_uint(storage::game_start_time, 32)
                    .store_uint(storage::game_round, 32) ;; Ğ¾Ñ‚cÑ‡ĞµÑ‚ Ñ 1 
                    .store_uint(storage::stop_game_on_round, 32) ;; 0
                    .store_coins(storage::jackpot)
                    .store_slice(storage::last_game_winner_address)
                    .store_dict(storage::list_participates)
                    .store_dict(storage::last_game_list_participates)
                .end_cell())
        .end_cell()
    );
}

() custom_randomize() impure inline {
    int prev_root_hash = 0;
    try {
        tuple prev_block = get_prev_block(); ;; (int wc, int shard, int seqno, int root_hash, int file_hash)
        prev_root_hash = prev_block.at(3);
    } catch (x, n) {
        prev_root_hash = 1;
    }

    randomize(prev_root_hash);
}

() send_message_with_sms(slice to, int amount, slice msg, int mode) impure inline {

    cell msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(to)
        .store_coins(amount)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(0, 32)
        .store_slice(msg)
    .end_cell();

    send_raw_message(msg, mode);
}


() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }

    slice sender_address = cs~load_msg_addr();
    
    load_data();

    int op = 0;

    if (in_msg_body.slice_empty?() == false) {
        op = in_msg_body~load_uint(32); ;; op-code
    }

    if (op == 0) { ;; Ğ¿Ñ€Ğ¾Ğ²ĞµÑ€ĞºĞ° Ğ½Ğ° ÑÑ‚Ğ°Ğ²ĞºÑƒ Ğ¸ Ğ½Ğ° ĞºĞ¾Ğ»Ğ¸Ñ‡ĞµÑÑ‚Ğ²Ğ¾ ÑƒÑ‡Ğ°ÑÑ‚Ğ½Ğ¸ĞºĞ¾Ğ²
        if (storage::game_round == storage::stop_game_on_round) {
            send_message_with_sms(sender_address, 0, "Game paused ğŸ”´", 64 + 2);
            return ();
        }
        if ((storage::min_bet > msg_value) | (storage::max_bet < msg_value)) { ;; need update sms with min and max bet
            send_message_with_sms(sender_address, 0, "Too little or too much TON âŒ", 64 + 2); 
            return ();
        }
        if (storage::participates_number >= storage::max_participates) { ;; there is a mini-problem => if there are already 100 participants, the participant cannot scale the bet by adding a ton
            send_message_with_sms(sender_address, 0, "Too many players ğŸ‘¥", 64 + 2);
            return ();
        }
        if (storage::game_start_time > now()) {
            send_message_with_sms(sender_address, 0, "Game starting soon â³", 64 + 2);
            return ();
        }

        var (_, key_addr) = parse_std_addr(sender_address);

        ;; var (value, found) = storage::list_participates~udict_add_get_ref?(256, key_addr, begin_cell().store_coins(msg_value).store_slice(sender_address).end_cell());
        var (payload, found) = storage::list_participates~udict_add_get_builder?(256, key_addr, begin_cell().store_coins(msg_value).store_slice(sender_address));

        if ( ~ found) {
            ;; slice payload = value.begin_parse();
            int new_bet = payload~load_coins() + msg_value;
            ;; storage::list_participates.udict_replace_ref?(256, key_addr, begin_cell().store_coins(new_bet).store_slice(sender_address).end_cell());
            storage::list_participates.udict_replace_builder?(256, key_addr, begin_cell().store_coins(new_bet).store_slice(sender_address));
        } else {
            storage::participates_number += 1; 
        }

        storage::jackpot += msg_value;
        save_data();
        return ();
    }

    ;; admin part

    throw_unless(error::not_admin, equal_slices_bits(sender_address, storage::admin_address));

    if (op == op::stop_game) { ;; stop game
        storage::stop_game_on_round = in_msg_body~load_uint(32);
        save_data();
        return ();
    }

    if (op == op::change_sittings) { ;; change basic sittings 
        storage::game_time = in_msg_body~load_uint(16);
        storage::min_bet = in_msg_body~load_coins();
        storage::max_bet = in_msg_body~load_coins();
        storage::max_participates = in_msg_body~load_uint(8);
        storage::commission_game_admin = in_msg_body~load_uint(16);
        storage::commission_game_runners = in_msg_body~load_uint(16);
        storage::game_start_time = in_msg_body~load_uint(32);
        save_data();
        return ();
    }

    if (op == op::change_admin) { ;; change admin
        storage::admin_address = in_msg_body~load_msg_addr();
        save_data();
        return ();
    }

    if (op == op::update_code_and_data) { ;; update code and data
        (cell new_data, cell new_code) = (in_msg_body~load_ref(), in_msg_body~load_ref());
        set_data(new_data);
        set_code(new_code);
        return ();
    }

    throw(0xffff);
}


() recv_external(slice in_msg_body) impure { ;; finish and start new game
    cell msg_inner = in_msg_body~load_ref();
    slice signature = in_msg_body~load_bits(512);
    in_msg_body.end_parse();
    int msg_inner_hash = msg_inner.cell_hash();

    load_data();

    throw_unless(error::invalid_signature, check_signature(msg_inner_hash, signature, storage::public_key));

    slice msg_inner_slice = msg_inner.begin_parse();  
    int game_number = msg_inner_slice~load_uint(32);
    var (game_runner, runne_exists) = msg_inner_slice~try_load_msg_addr();
    msg_inner_slice.end_parse();
    
    throw_unless(error::wrong_round, storage::game_round == game_number);
    throw_if(error::game_pause, storage::stop_game_on_round == game_number);
    throw_unless(error::game_is_not_over_yet, (storage::game_start_time + storage::game_time) <= now());

    accept_message();
    
    if (storage::participates_number == 0) { ;; Ğ·Ğ°Ğ½Ğ¾Ğ²Ğ¾ Ğ·Ğ°Ğ¿ÑƒÑĞºĞ°ĞµĞ¼ Ñ‚Ğ°Ğ¹Ğ¼ĞµÑ€

        storage::list_participates = new_dict();
        storage::last_game_list_participates = new_dict();
        storage::jackpot = 0;
        storage::participates_number = 0;
        storage::game_round += 1;
        storage::game_start_time = now();
        storage::last_game_winner_address = address_none();
        save_data();
        return ();

    } elseif (storage::participates_number == 1) { ;; Ğ²Ğ¾Ğ·Ñ€Ğ°Ñ‰Ğ°ĞµĞ¼ ÑÑ‚Ñ€ĞµĞ´ÑÑ‚Ğ²Ğ°

        ;; var (_, value, _) = storage::list_participates~udict_delete_get_min_ref?(256);
        ;; slice payload = value.begin_parse();
        var (_, payload, _) = storage::list_participates~udict_delete_get_min?(256);
        payload~load_coins();
        slice participate_address = payload~load_msg_addr();
        raw_reserve(storage_commission, 0); ;; 0.05 ton 
        send_message_with_sms(participate_address, 0, "Need more players ğŸš¶", 128 + 2);

        storage::list_participates = new_dict();
        storage::last_game_list_participates = new_dict();
        storage::jackpot = 0;
        storage::participates_number = 0;
        storage::game_round += 1;
        storage::game_start_time = now();
        storage::last_game_winner_address = address_none();
        save_data();
        return ();

    } else { ;; Ğ¸Ğ³Ñ€Ğ°

        custom_randomize();
        randomize_lt();
        int r = rand(storage::jackpot);

        storage::last_game_list_participates = storage::list_participates;

        int flag = 0;
        int sum_bet = 0;

        do {
            ;; var (_, value, _) = storage::list_participates~udict_delete_get_min_ref?(256);
            ;; slice payload = value.begin_parse();
            var (_, payload, _) = storage::list_participates~udict_delete_get_min?(256);
            sum_bet += payload~load_coins();
            if (r < sum_bet) {
                storage::last_game_winner_address = payload~load_msg_addr();
                flag = -1;
            }
        } until (flag == -1);

        ;; ÑĞ½Ğ°Ñ‡Ğ°Ğ»Ğ° Ğ¾Ñ‚Ğ¿Ñ€Ğ°Ğ²Ğ»ÑĞµĞ¼ Ğ´ĞµĞ½ÑŒĞ³Ğ¸ ĞµÑĞ»Ğ¸ ĞµÑÑ‚ÑŒ runner
        ;; Ğ¿Ğ¾Ñ‚Ğ¾Ğ¼ Ğ¾Ñ‚Ğ¿Ñ€Ğ°Ğ²Ğ»ÑĞµĞ¼ Ğ´ĞµĞ½ÑŒĞ³Ğ¸ Ğ²Ğ»Ğ°Ğ´ĞµĞ»ÑŒÑ†Ğ°Ğ¼
        ;; Ñ„Ğ¸Ğ½Ğ°Ğ»Ğµ Ğ¾Ñ‚Ğ¿Ñ€Ğ°Ğ²Ğ»ÑĞµĞ¼ Ğ´ĞµĞ½ÑŒĞ³Ğ¸ Ğ¿Ğ¾Ğ±ĞµĞ´Ğ¸Ñ‚ĞµĞ»Ñ 

        if (runne_exists) {
            int commission_runner = muldiv(storage::jackpot, storage::commission_game_runners, commission_divider); ;; Ğ´Ğ¾Ğ±Ğ°Ğ²Ğ¸Ñ‚ÑŒ ĞºĞ¾Ğ½ÑÑ‚Ğ°Ğ½Ñ‚Ñƒ 3.2% = 320
            storage::jackpot -= commission_runner;
            send_message_with_sms(game_runner, commission_runner, "Runner fee ğŸƒ", 2 + 1);
        }

        int commission_admin = muldiv(storage::jackpot, storage::commission_game_admin, commission_divider);
        send_message_with_sms(storage::admin_address, commission_admin, "Admin fee ğŸ’°", 2 + 1);

        raw_reserve(storage_commission, 0); ;; 0.05 ton 
        send_message_with_sms(storage::last_game_winner_address, 0, "You win ğŸ‰", 128 + 2);

        
        storage::list_participates = new_dict();
        storage::jackpot = 0;
        storage::participates_number = 0;
        storage::game_round += 1;
        storage::game_start_time = now();
        save_data();
        return ();
    }
}

(slice, int, int, int, int, int, int, int, int, int, int, int, int, slice, cell, cell) get_storage_data() method_id {
    load_data();
    return (
        storage::admin_address,
        storage::public_key,
        storage::commission_game_admin,
        storage::commission_game_runners,
        storage::min_bet,
        storage::max_bet,
        storage::max_participates,
        storage::participates_number,
        storage::game_time,
        storage::game_start_time,
        storage::game_round,
        storage::stop_game_on_round,
        storage::jackpot,
        storage::last_game_winner_address,
        storage::list_participates,
        storage::last_game_list_participates
    );
}